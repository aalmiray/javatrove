/*
 * Copyright 2016 Andres Almiray
 *
 * This file is part of Java Trove Examples
 *
 * Java Trove Examples is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Java Trove Examples is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Java Trove Examples. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.14.0'
    id 'com.github.hierynomus.license' version '0.11.0'
    id 'org.kordamp.gradle.stats' version '0.2.0'
}

repositories {
    jcenter()
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"

    compile "net.engio:mbassador:$mbassadorVersion"
    compile "org.reactfx:reactfx:2.0-M5"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile 'javax.inject:javax.inject:1'
    compile "org.springframework:spring-context:$springVersion"
    compile "org.kordamp.bootstrapfx:bootstrapfx-core:$bootstrapfxVersion"
    compile "org.kordamp.ikonli:ikonli-javafx:$ikonliVersion"
    compile "org.kordamp.ikonli:ikonli-typicons-pack:$ikonliVersion"
    compile "com.miglayout:miglayout-javafx:$miglayoutVersion"

    runtime "org.slf4j:slf4j-simple:$slf4jVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testCompile "org.awaitility:awaitility:$awaitilityVersion"
    testCompile "com.github.tomakehurst:wiremock:$wiremockVersion"
    testCompile "org.testfx:testfx-core:$testfxVersion"
    testCompile "org.testfx:testfx-junit:$testfxVersion"
}

mainClassName = 'org.kordamp.javatrove.example04.Launcher'

license {
    header = rootProject.file('gradle/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java = 'SLASHSTAR_STYLE'
        fxml = 'XML_STYLE'
    }
    ext.year    = '2016'
    ext.author  = 'Andres Almiray'
    ext.project = 'Java Trove Examples'
}

jacoco {
    toolVersion = '0.7.7.201606060606'
}

idea {
    project {
        jdkName "1.8"
        languageLevel "1.8"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'

                def compilerConfiguration = node.component.find { it.'@name' == 'CompilerConfiguration' }
                compilerConfiguration.remove(compilerConfiguration.annotationProcessing)
                compilerConfiguration.append(new XmlParser().parseText("""
    <annotationProcessing>
      <profile default="true" name="Default" enabled="true">
        <processorPath useClasspath="true" />
      </profile>
    </annotationProcessing>"""))
            }
        }
    }
}
