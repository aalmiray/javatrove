/*
 * Copyright 2016-2017 Andres Almiray
 *
 * This file is part of Java Trove Examples
 *
 * Java Trove Examples is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Java Trove Examples is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Java Trove Examples. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'groovy'
    id 'application'
    id 'idea'
    id 'jacoco'
    id 'net.ltgt.apt'                  version '0.10'
    id 'com.github.ben-manes.versions' version '0.15.0'
    id 'com.github.hierynomus.license' version '0.11.0'
    id 'org.kordamp.gradle.stats'      version '0.2.0'
}

repositories {
    jcenter()
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'pr', 'atlassian'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

dependencies {
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    compileOnly     "org.projectlombok:lombok:$lombokVersion"
    apt             "org.projectlombok:lombok:$lombokVersion"
    testApt         "org.projectlombok:lombok:$lombokVersion"

    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile("com.google.inject:guice:$guiceVersion") {
        exclude group: 'org.sonatype.sisu.inject', module: 'cglib'
    }
    compile 'ru.vyarus:guice-ext-annotations:1.2.1'
    compile "org.kordamp.bootstrapfx:bootstrapfx-core:$bootstrapfxVersion"
    compile "org.kordamp.ikonli:ikonli-javafx:$ikonliVersion"
    compile "org.kordamp.ikonli:ikonli-typicons-pack:$ikonliVersion"
    compile "com.miglayout:miglayout-javafx:$miglayoutVersion"

    runtime "org.slf4j:slf4j-simple:$slf4jVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.jukito:jukito:$jukitoVersion"
    testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testCompile "org.awaitility:awaitility:$awaitilityVersion"
    testCompile "org.testfx:testfx-core:$testfxVersion"
    testCompile "org.testfx:testfx-junit:$testfxVersion"
    testCompile "pl.pragmatists:JUnitParams:$junitParamsVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile "com.google.truth:truth:$truthVersion"
    testCompile "org.jgotesting:jgotesting:$jgotestingVersion"
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile("org.spockframework:spock-guice:$spockVersion") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile( 'com.athaydes:spock-reports:1.3.2' ) {
        transitive = false
    }
}

mainClassName = 'org.kordamp.javatrove.hello.pmvc.Launcher'
compileGroovy.enabled = false

license {
    header = rootProject.file('gradle/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java = 'SLASHSTAR_STYLE'
        fxml = 'XML_STYLE'
    }
    ext.year    = '2016-2017'
    ext.author  = 'Andres Almiray'
    ext.project = 'Java Trove Examples'
}

jacoco {
    toolVersion = '0.7.9'
}

idea {
    project {
        jdkName '1.8'
        languageLevel '1.8'
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'

                def compilerConfiguration = node.component.find { it.'@name' == 'CompilerConfiguration' }
                compilerConfiguration.remove(compilerConfiguration.annotationProcessing)
                compilerConfiguration.append(new XmlParser().parseText("""
    <annotationProcessing>
      <profile default="true" name="Default" enabled="true">
        <processorPath useClasspath="true" />
      </profile>
    </annotationProcessing>"""))
            }
        }
    }
}
